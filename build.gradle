buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.readdle.android.swift'
archivesBaseName = 'gradle'
version = '1.4.0'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "commons-configuration:commons-configuration:1.10"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

/**
 *
 * @param propertyName
 * @return returns the value as passed in from the command line as a -P, read from local.properties or System.getenv in that order
 */
String getFromEnvironment(String propertyName) {
    def properties = new Properties()
    try {
        properties.load(new FileInputStream(rootProject.file("local.properties")))
    } catch (Throwable e) {
        println "Cannot read local.properties"
    }
    def value = properties[propertyName] ?: System.getenv(propertyName)
    if (project.hasProperty(propertyName)) {
        value = project.getProperty(propertyName)
    }
    return value
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/"+getFromEnvironment('github.organization')+"/swift-android-gradle")
            credentials {
                username = getFromEnvironment('gpr.user')
                password = getFromEnvironment('gpr.key')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId archivesBaseName
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    // define this in ~/.gradle/gradle.properties
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    publish = true
    publications = ['mavenJava'] //When uploading Maven-based publication files

    pkg {
        repo = 'maven'
        userOrg = 'heliumfoot'
        name = 'swift-android-gradle'

        desc = 'Gradle plugin for build swift for android with Gradle and Swift Package Manager'
        vcsUrl = 'https://github.com/heliumfoot/swift-android-gradle'
        githubRepo = 'heliumfoot/swift-android-gradle'

        licenses = ['Apache-2.0']

        labels = ['gradle', 'plugin', 'swift', 'android']
        publicDownloadNumbers = true

        attributes = ['maturity': 'Experimental']
    }
}

bintrayUpload.dependsOn(build, sourcesJar)
